在之前的课程中我们研究了一些基本的Solidity语法。涵盖了变量、数据类型、函数、循环、条件流和数组。

然而，Solidity还有一些东西，这些东西在以后的编码中会很重要。在本教程中，我们将介绍一些更重要的Solidity主题。

## Mappings

Solidity 中的映射就像其他编程语言中的哈希图或字典。它们被用来存储键值对中的数据。

映射是用语法`mapping (keyType => valueType) `创建的。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract Mapping {
    //  从address类型映射到uint类型
    mapping(address => uint) public myMap;

    function get(address _addr) public view returns (uint) {
        // Mapping 总是会返回一个值
        // 如果这个值没有被设置过，则会返回默认值
        // uint类型的默认值是0
        return myMap[_addr];
    }

    function set(address _addr, uint _i) public {
        // 更新地址上的值
        myMap[_addr] = _i;
    }

    function remove(address _addr) public {
        // 将地址上的值设为默认值
        delete myMap[_addr];
    }
}
```

我们还可以创建嵌套映射，其中的键指向第二个嵌套映射。要做到这一点，我们将valueType设置为映射本身。

```solidity
contract NestedMappings {
    // Mapping from address => (mapping from uint to bool)
    mapping(address => mapping(uint => bool)) public nestedMap;

    function get(address _addr1, uint _i) public view returns (bool) {
        // You can get values from a nested mapping
        // even when it is not initialized
        // The default value for a bool type is false
        return nestedMap[_addr1][_i];
    }

    function set(
        address _addr1,
        uint _i,
        bool _boo
    ) public {
        nestedMap[_addr1][_i] = _boo;
    }

    function remove(address _addr1, uint _i) public {
        delete nestedMap[_addr1][_i];
    }
}
```

## Enum

Enum这个词代表了Enumerable。它们是用户定义的类型，包含一组常量的可读名称，称为成员。它们通常被用来限制一个变量只能有几个预定义的值中的一个。由于它们只是人类可读常量的一个抽象，实际上，它们在内部被表示为uint。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract Enum {
    // Enum representing different possible shipping states
    enum Status {
        Pending,
        Shipped,
        Accepted,
        Rejected,
        Canceled
    }

    // Declare a variable of the type Status
    // This can only contain one of the predefined values
    Status public status;

    // Since enums are internally represented by uints
    // This function will always return a uint
    // Pending = 0
    // Shipped = 1
    // Accepted = 2
    // Rejected = 3
    // Canceled = 4
    // Value higher than 4 cannot be returned
    function get() public view returns (Status) {
        return status;
    }

    // Pass the desired Status enum value as a uint
    function set(Status _status) public {
        status = _status;
    }

    // Update status enum value to a specific enum member, in this case, to the Canceled enum value
    function cancel() public {
        status = Status.Canceled; // Will set status = 4
    }
}
```

## Structs

结构的概念存在于许多高级编程语言中。它们被用来定义你自己的数据类型，将相关数据组合在一起。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract TodoList {
    // Declare a struct which groups together two data types
    struct TodoItem {
        string text;
        bool completed;
    }

    // Create an array of TodoItem structs
    TodoItem[] public todos;

    function createTodo(string memory _text) public {
        // There are multiple ways to initialize structs

        // Method 1 - Call it like a function
        todos.push(TodoItem(_text, false));

        // Method 2 - Explicitly set its keys
        todos.push(TodoItem({ text: _text, completed: false }));

        // Method 3 - Initialize an empty struct, then set individual properties
        TodoItem memory todo;
        todo.text = _text;
        todo.completed = false;
        todos.push(todo);
    }

    // Update a struct value
    function update(uint _index, string memory _text) public {
        todos[_index].text = _text;
    }

    // Update completed
    function toggleCompleted(uint _index) public {
        todos[_index].completed = !todos[_index].completed;
    }
}
```

## View and Pure Functions

你可能已经注意到，我们所写的一些函数在函数头中指定了一个`view`或`pure`。这些都是特殊的关键字，表示函数的特定行为。

Getter函数（那些返回值的函数）可以被声明为`view或`pure`。

View：不改变任何状态值的函数

Pure：不改变任何状态值的函数，但也不读取任何状态值

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract ViewAndPure {
    // Declare a state variable
    uint public x = 1;

    // Promise not to modify the state (but can read state)
    function addToX(uint y) public view returns (uint) {
        return x + y;
    }

    // Promise not to modify or read from state
    function add(uint i, uint j) public pure returns (uint) {
        return i + j;
    }
}

```

## Function Modifiers

修改器是可以在一个函数调用之前和/或之后运行的代码。它们通常用于限制对某些函数的访问，验证输入参数，防止某些类型的攻击，等等。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract Modifiers {
    address public owner;

    constructor() {
        // Set the contract deployer as the owner of the contract
        owner = msg.sender;
    }

    // Create a modifier that only allows a function to be called by the owner
    modifier onlyOwner() {
        require(msg.sender == owner, "You are not the owner");

        // Underscore is a special character used inside modifiers
        // Which tells Solidity to execute the function the modifier is used on
        // at this point
        // Therefore, this modifier will first perform the above check
        // Then run the rest of the code
        _;
    }

    // Create a function and apply the onlyOwner modifier on it
    function changeOwner(address _newOwner) public onlyOwner {
        // We will only reach this point if the modifier succeeds with its checks
        // So the caller of this transaction must be the current owner
        owner = _newOwner;
    }
}
```

## Events

事件允许合约在以太坊区块链上执行日志记录。例如，一个给定合约的日志可以在以后被解析，以便在前台界面上执行更新。它们通常被用来让前端界面监听特定的事件并更新用户界面，或者被用作廉价的存储形式。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract Events {
    // Declare an event which logs an address and a string
    event TestCalled(address sender, string message);

    function test() public {
        // Log an event
        emit TestCalled(msg.sender, "Someone called test()!");
    }
}
```

## Constructors

`constructor`函数是一个可选的函数，在合同首次部署时执行。你也可以向构造函数传递参数。

P.S.--如果你还记得，我们在大一的加密货币和NFT教程中实际上使用了构造函数!

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract X {
    string public name;

    // You will need to provide a string argument when deploying the contract
    constructor(string memory _name) {
        // This will be set immediately when the contract is deployed
        name = _name;
    }
}
```

## Inheritance

继承是一个合约可以继承另一个合约的属性和方法的程序。Solidity 支持多重继承。契约可以通过使用` is` 关键字来继承其他契约。

一个父合约如果有一个可以被子合约重写的函数，必须被声明为一个虚拟函数。

一个要覆盖父函数的子合约必须使用覆盖关键字。

如果父合同共享同名的方法或属性，那么继承的顺序很重要。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

/* Graph of inheritance
  A
 /  \
B    C
|\  /|
| \/ |
| /\ | 
D    E

*/

contract A {
    // Declare a virtual function foo() which can be overridden by children
    function foo() public pure virtual returns (string memory) {
        return "A";
    }
}

contract B is A {
    // Override A.foo();
    // But also allow this function to be overridden by further children
    // So we specify both keywords - virtual and override
    function foo() public pure virtual override returns (string memory) {
        return "B";
    }
}

contract C is A {
    // Similar to contract B above
    function foo() public pure virtual override returns (string memory) {
        return "C";
    }
}

// When inheriting from multiple contracts, if a function is defined multiple times, the right-most parent contract's function is used.
contract D is B, C {
    // D.foo() returns "C"
    // since C is the right-most parent with function foo();
    // override (B,C) means we want to override a method that exists in two parents
    function foo() public pure override (B, C) returns (string memory) {
        // super is a special keyword that is used to call functions
        // in the parent contract
        return super.foo();
    }
}

contract E is C, B {
    // E.foo() returns "B"
    // since B is the right-most parent with function foo();
    function foo() public pure override (C, B) returns (string memory) {
        return super.foo();
    }
}
```

## Transferring ETH

有三种方法可以将ETH从一个合约转移到其他地址。然而，其中两种方法不再是Solidity最新版本中的推荐方法，因此我们将跳过这些方法。

目前，从合约中转移ETH的推荐方法是使用`call`函数。调用函数返回一个bool，表示转移的成功或失败。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract SendEther {
    function sendEth(address payable _to) public payable {
        // Just forward the ETH received in this payable function
        // to the given address
        uint amountToSend = msg.value;
        // call returns a bool value specifying success or failure
        (bool success, bytes memory data) = _to.call{value: msg.value}("");
        require(success == true, "Failed to send ETH");
    }
}
```

### 如何在一个普通的以太坊账户地址中接收以太币

如果将ETH转移到普通账户（如Metamask地址），你不需要做任何特别的事情，因为所有这些账户都可以自动接受ETH转移。

### 如何在合约中接收以太币

但是，如果你写的合同希望能够直接接收ETH转账，你必须至少具备以下功能之一

- `receive() external payable`
- `fallback() external payable`

如果`msg.data`为空值则调用`receive()`，否则使用`fallback()`。

> msg.data是一种在交易中指定任意数据的方式。你通常不会手动使用它。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract ReceiveEther {
    /*
    Which function is called, fallback() or receive()?

           send Ether
               |
         msg.data is empty?
              / \
            yes  no
            /     \
receive() exists?  fallback()
         /   \
        yes   no
        /      \
    receive()   fallback()
    */

    // Function to receive Ether. msg.data must be empty
    receive() external payable {}

    // Fallback function is called when msg.data is not empty
    fallback() external payable {}

    function getBalance() public view returns (uint) {
        return address(this).balance;
    }
}
```

## Calling External Contracts

契约可以通过调用其他契约的实例上的函数来调用其他契约，比如A.foo(x, y, z)。要做到这一点，你必须有一个A的接口，告诉你的合约存在哪些函数。Solidity 中的接口表现得像头文件，它的作用与我们从前端调用合约时使用的 ABI 类似。这允许合约知道如何对函数参数和返回值进行编码和解码，以便调用外部合约。

注意：你使用的接口不需要很广泛，也就是说，它们不一定要包含外部合约中存在的所有函数--只包含那些你可能会在某个时候调用的函数。

假设有一个外部ERC20合约，我们有兴趣调用balanceOf函数来检查我们合约中的一个给定地址的余额。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;


interface MinimalERC20 {
    // Just include the functions we are interested in
    // in the interface
    function balanceOf(address account) external view returns (uint256);
}


contract MyContract {
    MinimalERC20 externalContract;


    constructor(address _externalContract) {
        // Initialize a MinimalERC20 contract instance
        externalContract = MinimalERC20(_externalContract);
    }


    function mustHaveSomeBalance() public {
        // Require that the caller of this transaction has a non-zero
        // balance of tokens in the external ERC20 contract
        uint balance = externalContract.balanceOf(msg.sender);
        require(balance > 0, "You don't own any tokens of external contract");
    }
}
```

## Import Statements

为了保持代码的可读性，您可以将您的 Solidity 代码分割到多个文件中。Solidity 允许导入本地和外部文件。

### 本地导入

假设我们有一个这样的文件夹结构：

```undefined
├── Import.sol
└── Foo.sol
```

Foo.sol代码如下

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

contract Foo {
    string public name = "Foo";
}
```

我们可以导入Foo并在Import.sol中这样使用它

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

// import Foo.sol from current directory
import "./Foo.sol";

contract Import {
    // Initialize Foo.sol
    Foo public foo = new Foo();

    // Test Foo.sol by getting it's name.
    function getFooName() public view returns (string memory) {
        return foo.name();
    }
}
```

注意：当我们使用Hardhat时，我们也可以通过npm将合同安装为node模块，然后从node_modules文件夹中导入合同。这些也算作本地导入，因为从技术上讲，当你安装一个包时，你是在将合同下载到你的本地机器上。

### External Imports

你也可以通过简单地复制URL从Github导入

```solidity
// https://github.com/owner/repo/blob/branch/path/to/Contract.sol
import "https://github.com/owner/repo/blob/branch/path/to/Contract.sol";


// Example import ERC20.sol from openzeppelin-contract repo
// https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol";
```

## Solidity Libraries

库类似于 Solidity 中的合约，但有一些限制。库不能包含任何状态变量，也不能转移ETH。

通常情况下，库是用来为你的合约添加辅助功能的。在Solidity世界中，一个非常常用的库是SafeMath--它可以确保数学运算不会导致整数下溢或溢出。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

library SafeMath {
    function add(uint x, uint y) internal pure returns (uint) {
        uint z = x + y;
        // If z overflowed, throw an error
        require(z >= x, "uint overflow");
        return z;
    }
}

contract TestSafeMath {
    function testAdd(uint x, uint y) public pure returns (uint) {
        return SafeMath.add(x, y);
    }
}
```

